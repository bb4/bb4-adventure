// Copyright by Barry G. Becker, 2016 - 2019. Licensed under MIT License: http://www.opensource.org/licenses/MIT
plugins {
    id "com.github.maiflai.scalatest" version "0.23" // needed to run scala tests
}
description = 'A framework for creating adventure games. It could also be used for other educational purposes.'
group = 'com.barrybecker4'
version = '1.6-SNAPSHOT'
apply from: 'https://raw.githubusercontent.com/bb4/bb4-common/master/bb4.gradle'

project.archivesBaseName = 'bb4-adventure'
mainClassName = "com.barrybecker4.puzzle.adventure.ui.GraphicalAdventure"


dependencies {
    compile 'com.barrybecker4:bb4-sound:1.6-SNAPSHOT'
    compile 'com.barrybecker4:bb4-ui:1.6-SNAPSHOT'
    // Used to generate html unit test report
    testRuntime 'org.pegdown:pegdown:1.6.0'
}

// The appMap is used to create the list of available application in the html index page and to generate the app tasks.
ext.appMap = new LinkedHashMap()
appMap['adventure'] = [
        title: 'Ludlow Adventure',
        mainClass: 'com.barrybecker4.puzzle.adventure.ui.GraphicalAdventure',
        resourcePath: 'com/barrybecker4/puzzle/adventure/stories/ludlow/',
        script: 'ludlowScript.xml',
        description: 'Explore a haunted house.',
        longDescription: 'This started as a simple text based adventure. The scenes are stored in an xml file. ' +
            'I thought it would be cool to have images and sounds associated with the scenes, so I added a UI. ' +
            'When editing the xml file got tedious, I decided to add the graphical editor. ' +
            'You can edit the story if you know the password. ' +
            'Next I may add support for keeping track of found items and hit-points. ' +
            'This story is based on an adventure published by James M. Ward in Dragon Magazine back in 1980.'
]
appMap['textAdventure'] = [
        title: 'Ludlow Text Adventure',
        mainClass: 'com.barrybecker4.puzzle.adventure.TextAdventure',
        resourcePath: 'com/barrybecker4/puzzle/adventure/stories/ludlow/',
        script: 'ludlowScript.xml',
        description: 'Explore a haunted house.',
        longDescription: 'The scenes are stored in an xml file. ' +
                'This story is based on an adventure published by James M. Ward in Dragon Magazine back in 1980.'
]
appMap['binaryTutorial'] = [
        title: 'Binary Tutorial',
        mainClass: 'com.barrybecker4.puzzle.adventure.ui.GraphicalAdventure',
        resourcePath: 'com/barrybecker4/puzzle/adventure/stories/learnBinary/',
        script: 'binaryScript.xml',
        description: 'Learn about binary numbers.',
        longDescription: 'The same framework that is used to make adventure games ' +
                'can also be used for pedagogical purposes. ' +
                'Here, the socratc method is used to teach about binary numbers.'
]
appMap['textBinaryTutorial'] = [
        title: 'Binary Tutorial (text version)',
        mainClass: 'com.barrybecker4.puzzle.adventure.TextAdventure',
        resourcePath: 'com/barrybecker4/puzzle/adventure/stories/learnBinary/',
        script: 'binaryScript.xml',
        description: 'Learn about binary numbers.',
        longDescription: 'The same framework that is used to make adventure games ' +
                'can also be used for pedagogical purposes.'
]


appMap.each { appId, propMap ->

    def mainClass = "com.barrybecker4.puzzle.adventure.ui.GraphicalAdventure"
    if (propMap.containsKey('mainClass'))
        mainClass = propMap['mainClass']

    def argsList = []
    if (propMap.containsKey('script'))
        argsList << "${propMap['script']}"
    if (propMap.containsKey('resourcePath'))
        argsList << "${propMap['resourcePath']}"

    tasks.create(name: "run${appId.capitalize()}", type: JavaExec, description: "${propMap.description}") {
        group = 'application'
        main = "${mainClass}"
        standardInput = System.in
        args = argsList
        classpath = sourceSets.main.runtimeClasspath
        jvmArgs = ["-ea"]
    }
}

ext.jarMap = new LinkedHashMap()
jarMap['adventure'] = [
        include:['com/barrybecker4/puzzle/adventure/**'],
        exclude:['com/barrybecker4/puzzle/adventure/**/*.pdf']
]


// The application is deployed to /build/distributions. Run scripts are in the bin directory.
// Additional script to run text version.
// It can take parameters like 'ludlow/ludlowScript.xml' or 'learnBinary/binaryScript.xml'
task createTextAdventureApp(type: CreateStartScripts) {
    mainClassName = "com.barrybecker4.puzzle.adventure.TextAdventure"
    classpath = startScripts.classpath
    outputDir = startScripts.outputDir
    applicationName = 'bb4-text-adventure'
    defaultJvmOpts = ["-Xmx1024m"]
}

// add extra execution scripts
applicationDistribution.into("bin") {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from(createTextAdventureApp)
    fileMode = 0755
}

apply from: 'https://raw.githubusercontent.com/bb4/bb4-common/master/bb4-publish.gradle'
apply from: 'https://raw.githubusercontent.com/bb4/bb4-common/master/bb4-deploy.gradle'
